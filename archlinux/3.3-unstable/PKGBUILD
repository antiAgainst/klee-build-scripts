# PKGBUILD file for klee-unstable (based on LLVM 3.3)

# Maintainer: Lei Zhang <antiagainst@gmail.com>

pkgname=klee-unstable
pkgver=3.3
pkgrel=1
pkgdesc='KLEE Symbolic Virtual Machine'
arch=('x86_64')
url='http://klee.github.io/klee/'
license=('custom:University of Illinois')
#groups=('klee')
depends=('python2' 'boost')
makedepends=('git' 'cmake' 'clang')
#provides=('llvm=3.3' 'clang=3.3' 'klee')
options=('!strip')
source=(http://llvm.org/releases/3.3/llvm-3.3.src.tar.gz
        http://llvm.org/releases/3.3/cfe-3.3.src.tar.gz
        stp::git+https://github.com/stp/stp.git
        klee-uclibc::git+https://github.com/klee/klee-uclibc.git#branch=klee_0_9_29
        klee::git+https://github.com/klee/klee.git)
md5sums=('40564e1dc390f9844f1711c08b08e391'
         '8284891e3e311829b8e44ac813d0c9ef'
         'SKIP' 'SKIP' 'SKIP')

_ncpu=$(grep -c processor /proc/cpuinfo)
_prefix=/usr/local

pkgver() {
    cd ${srcdir}/klee
    printf '3.3.r%s.%s' $(git rev-list --count HEAD) $(git rev-parse --short HEAD)
}

prepare() {
    cd ${srcdir}
    tar xzf llvm-3.3.src.tar.gz
    mv llvm-3.3{.src,}
    tar xzf cfe-3.3.src.tar.gz
    mv cfe-3.3.src llvm-3.3/tools/clang
}

_build_llvm_clang() {
    cd ${srcdir}/llvm-3.3
    ./configure \
        --enable-optimized \
        --prefix=${_prefix}
    make -j${_ncpu}
}

_build_stp() {
    cd ${srcdir}/stp
    mkdir build
    cd build
    cmake ../
    make -j${_ncpu}
}

_build_klee_uclibc() {
    cd ${srcdir}/klee-uclibc
    ./configure --make-llvm-lib
    make -j${_ncpu}
}

_build_klee() {
    cd ${srcdir}/klee
    ./configure \
        --with-llvm=${srcdir}/llvm-3.3 \
        --with-stp=${srcdir}/stp/build \
        --with-uclibc=${srcdir}/klee-uclibc \
        --enable-posix-runtime \
        --prefix=${_prefix}
    make -j${_ncpu} ENABLE_OPTIMIZED=1
}

build() {
    _build_llvm_clang
    _build_stp
    _build_klee_uclibc
    _build_klee
}

_test_klee() {
    cd ${srcdir}/klee
    #make -k test
    make -k unittests
}

check() {
    _test_klee
}

_install_llvm_clang() {
    cd ${srcdir}/llvm-3.3
    make prefix=${pkgdir}/${_prefix} install
}

_install_klee() {
    cd ${srcdir}/klee
    make prefix=${pkgdir}/${_prefix} install
}

package() {
    _install_llvm_clang
    _install_klee
}

# vim:set ts=4 sw=4 et:
